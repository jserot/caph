-- This is a rewriting of the [../basic/scale] example using io-less input and output actors
--
-- The input stream passed to the [scale] actor is produced by the [src] actor and
-- the output stream produced by this actor is handled by the [snk] actor.
-- We here provide an implementation of these actors in SystemC in which the corresponding data
-- is generated by the actor and written in a file resp.
--
-- Since 2.9.1
-- May 2019, JS

#pragma implemented(src,systemc,read_data)
#pragma implemented(snk,systemc,write_result)

actor src 
   in (i: unit)
  out (o: unsigned<8>)
var cnt: unsigned<8> = 0
rules
| i:_ -> (o:cnt, cnt:cnt+1)  -- this behavior will be overriden by the SystemC implementation
;

actor snk
   in (i: unsigned<8>)
  out (o: unit)
var cnt: unsigned<8> = 0
rules
| i:x -> cnt:cnt+x  -- this behavior will be overriden by the SystemC implementation
;

actor scale (k:unsigned<8>)
  in (i:unsigned<8>)
  out (o:unsigned<8>)
rules
| i:x -> o:x*k
;

net () = snk (scale 2 (src ()));
